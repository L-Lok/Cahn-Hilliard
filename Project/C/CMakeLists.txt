# Minimum version - 3.17
# Required for find_package(CUDAToolkit)
# Can be lowered if not using CUDA.
cmake_minimum_required(VERSION 3.17)

# Project name
project(LMECA2300 LANGUAGES C)

# Exec name
set(EXEC_NAME project)
set(CMAKE_C_FLAGS "-O3")

# CMake module
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/")

# Project files
include_directories(${PROJECT_SOURCE_DIR}/inc)
file(GLOB C_SRCS "${PROJECT_SOURCE_DIR}/src/*.c")

# Compile options
option(N_DISCR "Discretisation grid to use." OFF)
if (NOT N_DISCR)
    set(N_DISCR 128)
endif (NOT N_DISCR)
message(STATUS "Using a ${N_DISCR}x${N_DISCR} grid.")
add_definitions(-DN_DISCR=${N_DISCR})

option(SOLVER "Solver to use." OFF)
if (NOT SOLVER)
    set(SOLVER ETDRK4)
endif (NOT SOLVER)
message(STATUS "Using the ${SOLVER} solver.")
add_definitions(-DSOLVER=${SOLVER})

# Define some physical constants
set(KAPPA 1e-4)
add_definitions(-DKAPPA=${KAPPA})

# CPU or GPU (CUDA)
option(USE_CUDA "Compile GPU code, using CUDA." OFF)
if (USE_CUDA)
    # Enable CUDA
    message(STATUS "Compiling with CUDA...")
    enable_language(CUDA)

    # cuFFT & co
    find_package(CUDA REQUIRED)
    find_package(CUDAToolkit REQUIRED)
    set(EXT_LIBS ${EXT_LIBS} CUDA::cufft)
    add_definitions(-DUSE_CUDA)

    # GPU-specific files
    include_directories(${PROJECT_SOURCE_DIR}/inc/gpu)
    file(GLOB CUDA_SRCS "${PROJECT_SOURCE_DIR}/src/gpu/*.cu")
    file(GLOB C_SRCS ${C_SRCS} "${PROJECT_SOURCE_DIR}/src/gpu/*.c")

    # Compile *.cu source files
    cuda_compile(CUDA_O ${CUDA_SRCS})

else (USE_CUDA)
    # CPU-specific files
    include_directories(${PROJECT_SOURCE_DIR}/inc/cpu)
    file(GLOB C_SRCS ${C_SRCS} "${PROJECT_SOURCE_DIR}/src/cpu/*.c")

    # FFT libraries - MKL or FFTW3 (if MKL not found)
    find_package(MKL)
    if (MKL_FOUND)          # Intel's MKL
        include_directories(${MKL_INCLUDE_DIR})
        set(EXT_LIBS ${EXT_LIBS} ${MKL_LIBRARIES})
    else (MKL_FOUND)        # FFTW3
        message(STATUS "Intel's MKL not found -- proceeding with FFTW3.")
        find_package(FFTW REQUIRED)
        include_directories(${FFTW_INCLUDE_DIRS})
        set(EXT_LIBS ${EXT_LIBS} ${FFTW_LIBRARIES})
    endif (MKL_FOUND)
endif (USE_CUDA)

# GLEW
find_package(GLEW REQUIRED)
include_directories(${GLEW_INCLUDE_DIRS})
set(EXT_LIBS ${EXT_LIBS} ${GLEW_LIBRARIES})

# GLFW3
find_package(GLFW3 REQUIRED)
include_directories(${GLFW3_INCLUDE_DIR})
set(EXT_LIBS ${EXT_LIBS} ${GLFW3_LIBRARY})

# OpenGL
find_package(OpenGL REQUIRED)
include_directories(${OPENGL_INCLUDE_DIR})
set(EXT_LIBS ${EXT_LIBS} ${OPENGL_LIBRARY})

# Compile/link src files & libs
add_executable(${EXEC_NAME} ${C_SRCS} ${CUDA_O})
target_link_libraries(${EXEC_NAME} ${EXT_LIBS} m dl pthread)

# Target properties
set_property(TARGET ${EXEC_NAME} PROPERTY CUDA_ARCHITECTURES OFF)
